// Â© Timo Anttila, Tuspe Design Oy

//@version=6
indicator("Trend is your friend", shorttitle="Trend", overlay=true, max_labels_count=200)

// Inputs
group1 = "Numbers of bars for Longs"
entry_bull_bars = input.int(20, "Entry Long", group=group1)
entry_bear_bars = input.int(20, "Entry Short", group=group1)
exit_bear_bars = input.int(10, "Exit", group=group1)

group2 = "Average True Range"
atr_length = input.int(20, "ATR length", group=group2)
atr_exit_m = input.float(2, "ATR exit multiplier", group=group2)

group3 = "Moving Averages"
ma1_length = input.int(20, "SMA Short Length", maxval=50, minval=5, group=group3)
ma1 = ta.sma(close, ma1_length)
plot(ma1, color=color.yellow, linewidth=2, title="SMA Short")

ma2_length = input.int(100, "SMA Long Length", maxval=300, minval=20, group=group3)
ma2 = ta.sma(close, ma2_length)
plot(ma2, color=color.white, linewidth=2, title="SMA Long")

group4 = "Relative Strength Index"
rsi_period = input.int(14, "RSI Period", minval=5, group=group4)
rsi_max = input.float(80, "RSI Overbought", minval=50, maxval=90, group=group4)
rsi_min = input.float(20, "RSI Oversold", minval=10, maxval=50, group=group4)

group5 = "Entry Settings"
entry_max = input.int(3, "Max entries per trend", group=group5)
min_body_perc = input.float(50, "Min entry candle body %", minval=20, maxval=95, group=group5)
only_longs = input.bool(false, "Only Longs", group=group5)

// ----------
//  GLOBAL VARIABLES
// ----------

bool is_bear = close < open
bool is_bull = close > open
float atr_val = ta.atr(atr_length)
float entry_highest = ta.highest(high, entry_bull_bars)[1]
float entry_lowest = ta.lowest(low, entry_bear_bars)[1]
float exit_highest = ta.highest(high, exit_bear_bars)[1]
float exit_lowest = ta.lowest(low, exit_bear_bars)[1]
float rsi = ta.rsi(close, rsi_period)
float vol_avg = ta.sma(volume, 14)
int entry = na
int exit = na
var float atr_exit = na
var float entry_clear = na
var float pullback = na
var int entry_taken  = 0
var int exit_taken = 0
var int trend = 0

// ----------
//  EXIT / ATR FIX
// ----------

if entry_taken > 0 and entry_max > exit_taken
    if trend == 1 and (close < exit_lowest or close < atr_exit)
        exit := 1

    else if trend == 2 and (close > exit_highest or close > atr_exit)
        exit := 2

    if not na(exit)
        exit_taken := exit_taken + 1
        entry_taken := 0

// ----------
//  CROSS
// ----------

bool cross_bear = ta.crossunder(close, ma2)
bool cross_bull = ta.crossover(close, ma2)

if cross_bull or cross_bear
    if entry_taken > 0
        exit := trend

    entry_taken := 0
    exit_taken := 0
    pullback := na
    trend := cross_bull ? 1 : 2

// ----------
//  ENTRY
// ----------

float body_size = math.abs(close - open)
float candle_size = math.abs(high - low)

valid_candle() =>
    float upper_wick = high - math.max(open, close)
    float lower_wick = math.min(open, close) - low

    bool body_ok = candle_size > 0 ? body_size / candle_size >= min_body_perc / 100 : false
    bool atr_ok = body_size > 0 ? body_size > atr_val * 0.5 and candle_size < atr_val * 4 : false
    bool structure_ok = trend == 1 ? is_bull and close > high[1] and close > ma1 and close > ma2 : trend == 2 and is_bear and low < low[1] and low < ma1 and low < ma2

    body_ok and atr_ok and structure_ok and (volume > vol_avg or volume > volume[1])

new_high() =>
    trend == 1 ? close > entry_highest and (na(pullback) or close > pullback) : close < entry_lowest and (na(pullback) or close < pullback)

if entry_max > entry_taken and entry_max > exit_taken and rsi > rsi_min and rsi < rsi_max

    if entry_taken == 0 and valid_candle()
        if new_high()
            entry := trend

        else
            float upper_wick = high - math.max(open, close)
            float lower_wick = math.min(open, close) - low
            float wick_size = 0.2
            bool wick_ok = trend == 1 ? upper_wick / candle_size <= wick_size : lower_wick / candle_size <= wick_size
            float prev_candle_size = math.abs(high[1] - low[1])
            bool colors = trend == 1 ? is_bull and is_bear[1] : is_bear and is_bull[1]

            if colors and candle_size > atr_val and body_size > prev_candle_size * 1.2 and wick_ok
                entry := trend

    else if na(pullback)
        if trend == 1 and is_bear and (low < ma1 or low < ma2)
            pullback := high

        else if trend == 2 and is_bull and (high > ma1 or high > ma2)
            pullback := low

    else if valid_candle() and new_high()
        entry := trend

    if not na(entry)
        entry_taken := entry_taken + 1
        pullback := na

        float atr_sl_size = atr_exit_m * atr_val
        atr_exit := trend == 1 ? close - atr_sl_size : close + atr_sl_size

        if not na(exit)
            exit := na

// ----------
//  VISUAL
// ----------

plot_size = size.tiny
plot_color = color.new(color.white, 20)
plot_bull = location.belowbar
plot_bear = location.abovebar

plotshape(entry == 1 and entry_taken == 1, title="Long Entry", style=shape.triangleup, location=plot_bull, color=plot_color, size=plot_size)
plotshape(entry == 2 and entry_taken == 1, title="Short Entry", style=shape.triangledown, location=plot_bear, color=plot_color, size=plot_size)

plot_style = shape.cross

plotshape(entry == 1 and entry_taken > 1, title="Long Add", style=plot_style, location=plot_bull, color=plot_color, size=plot_size)
plotshape(entry == 2 and entry_taken > 1, title="Short Add", style=plot_style, location=plot_bear, color=plot_color, size=plot_size)

plot_style := shape.xcross
plot_color := color.new(color.gray, 20)

plotshape(exit == 1, title="Long Exit", style=plot_style, location=plot_bull, color=plot_color, size=plot_size)
plotshape(exit == 2, title="Short Exit", style=plot_style, location=plot_bear, color=plot_color, size=plot_size)

// ----------
//  ALERTS
// ----------

alertcondition(entry > 0, title="All entries", message="New signal found. Check where next candle is going before entering. Happy trading!")
alertcondition(entry > 0 and entry_taken == 1, title="New Trend", message="New trend found. Happy trading!")
alertcondition(entry > 0 and entry_taken > 1, title="Add More", message="It might be good time to add to your winner. Happy trading!")

// ----------
//  BROKER / EXCHANGE DAILY CANDLES
// ----------

bool isLTF = timeframe.in_seconds() <= 7200
pdh = request.security(syminfo.tickerid, "D", high[1])
pdl = request.security(syminfo.tickerid, "D", low[1])
todayOpen = request.security(syminfo.tickerid, "D", open)
dayStart = request.security(syminfo.tickerid, "D", time)
newDay = ta.change(dayStart) != 0 and isLTF
futureTime = time + 5 * (time - time[1])

// ----------
//  DRAW ON DAILY SESSION START
// ----------

var line pdhLine = na
var line pdlLine = na
var line openLine = na

var label pdhLabel = na
var label pdlLabel = na
var label openLabel = na

if newDay
    line.delete(pdhLine)
    line.delete(pdlLine)
    line.delete(openLine)

    label.delete(pdhLabel)
    label.delete(pdlLabel)
    label.delete(openLabel)

    // Previous Day High
    pdhLine := line.new(dayStart, pdh, futureTime, pdh,
        xloc = xloc.bar_time,
        extend = extend.none,
        color = color.red,
        style = line.style_dashed)

    // Previous Day Low
    pdlLine := line.new(dayStart, pdl, futureTime, pdl,
        xloc = xloc.bar_time,
        extend = extend.none,
        color = color.green,
        style = line.style_dashed)

    // Today Open
    openLine := line.new(dayStart, todayOpen, futureTime, todayOpen,
        xloc = xloc.bar_time,
        extend = extend.none,
        color = color.blue,
        width = 2)

    // Labels (right side, transparent)
    pdhLabel := label.new(futureTime, pdh, "PDH",
        xloc = xloc.bar_time,
        style = label.style_label_left,
        textcolor = color.red,
        color = color.new(color.red, 100))

    pdlLabel := label.new(futureTime, pdl, "PDL",
        xloc = xloc.bar_time,
        style = label.style_label_left,
        textcolor = color.green,
        color = color.new(color.green, 100))

    openLabel := label.new(futureTime, todayOpen, "OPEN",
        xloc = xloc.bar_time,
        style = label.style_label_left,
        textcolor = color.blue,
        color = color.new(color.blue, 100))